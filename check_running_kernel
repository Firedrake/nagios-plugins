#! /usr/bin/perl -w

# nagios: -epn
use strict;
use Monitoring::Plugin;
use Dpkg::Version;

my $np=Monitoring::Plugin->new(shortname => 'kernel',
                           usage => "Usage: %s ",
                           version => 0.1,
                           blurb => 'Tests running kernel against Debian package version',
                           url => 'https://github.com/Firedrake/nagios-plugins',
);

my $r;
{ # latest version that will be run at next boot
  opendir I,'/boot';
  my @vm=sort map {/vmlinuz-(.*)$/;Dpkg::Version->new($1)} grep /^vmlinuz/,readdir I;
  closedir I;
  $r=$vm[-1]->as_string;
}
if ($r) {
  # full dpkg version of latest version
  my $dpkgver=getoutput(qw(dpkg-query -W -f),'${Version}',"linux-image-$r");
  # full dpkg version of actual running version
  my $runver=getoutput(qw(uname -v));
  my %runver=map {$_ => 1} (split ' ',$runver);
  if (!exists $runver{$dpkgver}) {
    $np->plugin_exit(CRITICAL,"Running $runver, want $dpkgver");
  } else {
    $np->plugin_exit(OK,"Running $runver");
  }
}
$np->plugin_exit(UNKNOWN,"Can't get running version");

sub getoutput {
  open I,'-|',@_;
  chomp (my $r=join('',<I>));
  close I;
  return $r;
}
